
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// регистр ДополнительныеРасходы Расход
	Если УчитыватьДопРасходы Тогда
		
		Движения.ДополнительныеРасходы.Записывать = Истина;
		Движение 					= Движения.ДополнительныеРасходы.Добавить();
		Движение.ВидДвижения 		= ВидДвиженияНакопления.Расход;
		Движение.Период 			= Дата;
		Движение.Коллекционер 	= Коллекционер;
		Движение.Сумма 			= Список.Итог("ДопРасходы");
		
	КонецЕсли;	
	
	// регистр ДвиженияМонет Расход
	Движения.ДвиженияМонет.Записывать = Истина;
	Для Каждого ТекСтрокаСписок Из Список Цикл
		
		Движение 					= Движения.ДвиженияМонет.Добавить();
		Движение.ВидДвижения 		= ВидДвиженияНакопления.Расход;
		Движение.Период 			= Дата;
		Движение.Объект 			= ТекСтрокаСписок.Монета;
		Движение.Коллекция			= ТекСтрокаСписок.Коллекция;
		Движение.Количество		= ?(ТекСтрокаСписок.Количество > 0, ТекСтрокаСписок.Количество, 1);
				
		ИнформацияПоМонете		= РегистрыНакопления.ДвиженияМонет.ПолучитьИнформациюПоОбъекту(ТекСтрокаСписок.Монета, Дата); 
		Если ИнформацияПоМонете.Свойство ("Стоимость") Тогда
			Движение.Стоимость		= ИнформацияПоМонете.Стоимость;
		КонецЕсли;	
		
	КонецЦикла;
	
	// по регистру Продажи монет
	Движения.ПродажаМонет.Записывать = Истина;
	Для Каждого ТекСтрокаСписок Из Список Цикл
		
		Движение 					= Движения.ПродажаМонет.Добавить();
		Движение.Период 			= Дата;
		Движение.Объект 			= ТекСтрокаСписок.Монета;
		Движение.Валюта 			= ТипЦены.Валюта;
		Движение.Коллекция			= ТекСтрокаСписок.Коллекция;
		Движение.Количество		= ?(ТекСтрокаСписок.Количество > 0, ТекСтрокаСписок.Количество, 1);
		Движение.Сумма 			= ТекСтрокаСписок.Сумма;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Если ПроверяемыеРеквизиты.Найти ("ТипЦены") = Неопределено Тогда
		ПроверяемыеРеквизиты.Добавить ("ТипЦены")	
	КонецЕсли;	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	СуммаДокумента = Список.Итог("Сумма");
	
КонецПроцедуры
