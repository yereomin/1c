
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	// сначала запишем номера монет в коллекции
	ЗаписатьНомераМонетВКоллекции ();
	
	// регистр ДвиженияМонет
	Движения.ДвиженияМонет.Записывать = Истина;
	Для Каждого ТекСтрокаСписок Из Список Цикл
		
		Если ТекСтрокаСписок.УчетноеКоличество = ТекСтрокаСписок.ФактическоеКоличество Тогда
			Продолжить
		КонецЕсли;	
		
		// по регистру ДвиженияМонет
		Движение 				= Движения.ДвиженияМонет.Добавить();
		Движение.Период 		= Дата;
		Движение.Объект 		= ТекСтрокаСписок.Монета;
		Движение.Коллекция		= Коллекция;
		Движение.ВидДвижения 	= ?(ТекСтрокаСписок.УчетноеКоличество < ТекСтрокаСписок.ФактическоеКоличество, ВидДвиженияНакопления.Приход, ВидДвиженияНакопления.Расход);
		Движение.Количество		= Макс(ТекСтрокаСписок.УчетноеКоличество - ТекСтрокаСписок.ФактическоеКоличество, ТекСтрокаСписок.ФактическоеКоличество - ТекСтрокаСписок.УчетноеКоличество);
		
		Если Движение.ВидДвижения = ВидДвиженияНакопления.Расход Тогда
			ИнформацияПоМонете		= РегистрыНакопления.ДвиженияМонет.ПолучитьИнформациюПоОбъекту(ТекСтрокаСписок.Монета, Дата); 
			Если ИнформацияПоМонете.Свойство ("Стоимость") Тогда
				Стоимость = ИнформацияПоМонете.Стоимость
			Иначе
				Стоимость = 0
			КонецЕсли;
		Иначе
			Стоимость = ТекСтрокаСписок.Цена 
		КонецЕсли;
		
		Движение.Стоимость		= Стоимость;

		// по регистру СтоимостьОбъектовКоллекционирования
		Если ТекСтрокаСписок.УчетноеКоличество < ТекСтрокаСписок.ФактическоеКоличество Тогда
			РегистрыСведений.СтоимостьОбъектовКоллекцинирования.ЗаписатьИнформациюОСтоимости(ТекСтрокаСписок.Монета, Справочники.ТипыЦен.Оценочная, Стоимость, Дата, Ссылка)
		КонецЕсли;	
		
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьНомераМонетВКоллекции ()
	Для Каждого Строка ИЗ Список Цикл
		Если Строка.НомерВКоллекции <> 0 Тогда
			РегистрыСведений.НомерВКоллекции.ЗаписатьНомер(Строка.Монета, Строка.НомерВКоллекции)
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры	
