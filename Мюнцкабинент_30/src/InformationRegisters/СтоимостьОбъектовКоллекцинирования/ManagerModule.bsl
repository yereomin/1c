Функция ПолучитьТаблицуЦенПоОбъекту(Объект) Экспорт
	
	Набор = РегистрыСведений.СтоимостьОбъектовКоллекцинирования.СоздатьНаборЗаписей();
	Набор.Отбор.Объект.Установить(Объект);
	Набор.Прочитать();
	Возврат Набор.Выгрузить();
	
КонецФункции

// Записывает набор записей в регистр сведений с отбором по объекту
//
Процедура ЗаписатьНаборПоОбъекту(Объект, ТаблицаИсточник) Экспорт
	
	Если Не ЗначениеЗаполнено(Объект) Тогда
		Возврат;
	КонецЕсли;
	
	Набор = РегистрыСведений.СтоимостьОбъектовКоллекцинирования.СоздатьНаборЗаписей();
	Набор.Отбор.Объект.Установить(Объект);
	
	Для каждого ИсточникСтрока Из ТаблицаИсточник Цикл
		Запись 						= Набор.Добавить();
		Запись.Объект 				= Объект;
		Запись.Период 				= ИсточникСтрока.Период;
		Запись.ТипЦены 				= ИсточникСтрока.ТипЦены;
		Запись.Стоимость			= ИсточникСтрока.Стоимость;
	КонецЦикла;
	
	Набор.Записать(Истина);
	
КонецПроцедуры

Процедура УдалитьЗаписиПоРегистраторуЦен (РегистраторЦен) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтоимостьОбъектовКоллекцинирования.Период,
		|	СтоимостьОбъектовКоллекцинирования.Объект,
		|	СтоимостьОбъектовКоллекцинирования.ТипЦены
		|ИЗ
		|	РегистрСведений.СтоимостьОбъектовКоллекцинирования КАК СтоимостьОбъектовКоллекцинирования
		|ГДЕ
		|	СтоимостьОбъектовКоллекцинирования.РегистраторЦен = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", РегистраторЦен);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	НЗ = РегистрыСведений.СтоимостьОбъектовКоллекцинирования.СоздатьНаборЗаписей();
	Пока Выборка.Следующий() Цикл
		НЗ.Отбор.Период.Установить (Выборка.Период);	
		НЗ.Отбор.Объект.Установить (Выборка.Объект);	
		НЗ.Отбор.ТипЦены.Установить (Выборка.ТипЦены);
		НЗ.Прочитать();
		НЗ.Очистить();
		НЗ.Записать();
	КонецЦикла;
	
	//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	
КонецПроцедуры

Процедура ЗаписатьИнформациюОСтоимости (МонетаСсылка, ТипЦены, Стоимость, Период = '0001-01-01', Регистратор = Неопределено) Экспорт
	
	Если Период = Дата (1,1,1) Тогда
		Период = ТекущаяДата()
	КонецЕсли;
	
	НЗ = РегистрыСведений.СтоимостьОбъектовКоллекцинирования.СоздатьНаборЗаписей();
	НЗ.Отбор.Период.Установить (Период);
	НЗ.Отбор.Объект.Установить (МонетаСсылка);
	НЗ.Отбор.ТипЦены.Установить (ТипЦены);
	
	НЗ.Прочитать();
	НЗ.Очистить();
	
	НоваяЗапись = НЗ.Добавить();
	НоваяЗапись.Период 				= Период;
	НоваяЗапись.Объект 				= МонетаСсылка;
	НоваяЗапись.ТипЦены 			= ТипЦены;
	НоваяЗапись.Стоимость 			= Стоимость;
	Если НЕ Регистратор = Неопределено Тогда
		НоваяЗапись.РегистраторЦен 	= Регистратор;
	Иначе
		НоваяЗапись.РегистраторЦен 	= ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;	
	
	НЗ.Записать();
	
КонецПроцедуры	